---
title: "オブジェクトの作成"
---

import { Callout } from 'nextra/components'

# オブジェクトの作成と設定

オブジェクトの作成には2つのステップがあります：

1. `ObjectCore`リソースグループの作成（後でオブジェクトを参照するために使用できるアドレスを持ちます）。
2. `Ref`と呼ばれる権限を使用してオブジェクトの動作をカスタマイズします。

<Callout type="info">
  `Ref`を生成してオブジェクトを設定することは、オブジェクトを作成するのと同じトランザクションで行う必要があります。
  後でこれらの設定を変更することはできません。
</Callout>

## オブジェクトの作成

作成できるオブジェクトには3つのタイプがあります：

1. **通常のオブジェクト。** このタイプは削除可能でランダムなアドレスを持ちます。以下を使用して作成できます：`0x1::object::create_object(owner_address: address)`。例：
```move filename="object_playground.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object;

  entry fun create_my_object(caller: &signer) {
    let caller_address = signer::address_of(caller);
    let constructor_ref = object::create_object(caller_address);
    // ...
  }
}
```

2. **名前付きオブジェクト。** このタイプは削除**不可能**で、決定論的なアドレスを持ちます。以下を使用して作成できます：`0x1::object::create_named_object(creator: &signer, seed: vector<u8>)`。例：
```move filename="object_playground.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object;

  /// 名前付きオブジェクトのシード、作成アカウントにおいてグローバルに一意である必要があります
  const NAME: vector<u8> = b"MyAwesomeObject";

  entry fun create_my_object(caller: &signer) {
    let caller_address = signer::address_of(caller);
    let constructor_ref = object::create_named_object(caller, NAME);
    // ...
  }

  #[view]
  fun has_object(creator: address): bool {
    let object_address = object::create_object_address(&creator, NAME);
    object::object_exists<0x1::object::ObjectCore>(object_address)
  }
}
```

3. **スティッキーオブジェクト。** このタイプも削除**不可能**で、ランダムなアドレスを持ちます。以下を使用して作成できます：`0x1::object::create_sticky_object(owner_address: address)`。例：
```move filename="object_playground.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object;

  entry fun create_my_object(caller: &signer) {
    let caller_address = signer::address_of(caller);
    let constructor_ref = object::create_sticky_object(caller_address);
    // ...
  }
}
```

## オブジェクト機能のカスタマイズ

オブジェクトを作成すると、追加の`Ref`を生成するために使用できる`ConstructorRef`を受け取ります。`Ref`は、リソースの転送、オブジェクト自体の転送、オブジェクトの削除など、特定のオブジェクト機能を有効化/無効化/実行するために将来使用できます。

以下のセクションでは、一般的に使用される`Ref`とそれらが有効にする機能について説明します。

<Callout type="info">
  注意：`ConstructorRef`は保存できません。オブジェクトを作成するために使用されたトランザクションの終了時に破棄されるため、
  他の`Ref`は必ずオブジェクト作成時に生成する必要があります。
</Callout>

### リソースの追加

`ConstructorRef`を`object::generate_signer`と共に使用して、オブジェクトにリソースを転送できるsignerを作成できます。これは、アカウントにリソースを追加する場合と同じ`move_to`関数を使用します。

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object;

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct MyStruct has key {
    num: u8
  }

  entry fun create_my_object(caller: &signer) {
    let caller_address = signer::address_of(caller);

    // オブジェクトを作成
    let constructor_ref = object::create_object(caller_address);

    // オブジェクトのsignerを取得
    let object_signer = object::generate_signer(&constructor_ref);

    // MyStructリソースをオブジェクトに移動
    move_to(&object_signer, MyStruct { num: 0 });

    // ...
  }
}
```

### 拡張性の追加（`ExtendRef`）

時にはオブジェクトを後で編集可能にしたい場合があります。その場合、`object::generate_extend_ref`を使用して`ExtendRef`を生成できます。このrefはオブジェクトのsignerを生成するために使用できます。

以下の例のように、スマートコントラクトのロジックを通じて`ExtendRef`の使用権限を制御できます。

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use std::string::{Self, String};
  use aptos_framework::object::{Self, Object};

  /// 呼び出し元がオブジェクトの所有者ではありません
  const E_NOT_OWNER: u64 = 1;
  /// 呼び出し元がコントラクトの公開者ではありません
  const E_NOT_PUBLISHER: u64 = 2;

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct MyStruct has key {
    num: u8
  }

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct Message has key {
    message: string::String
  }

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct ObjectController has key {
    extend_ref: object::ExtendRef,
  }

  entry fun create_my_object(caller: &signer) {
    let caller_address = signer::address_of(caller);

    // オブジェクトを作成
    let constructor_ref = object::create_object(caller_address);

    // オブジェクトのsignerを取得
    let object_signer = object::generate_signer(&constructor_ref);

    // MyStructリソースをオブジェクトに移動
    move_to(&object_signer, MyStruct { num: 0 });

    // extend_refを作成し、オブジェクトに移動
    let extend_ref = object::generate_extend_ref(&constructor_ref);
    move_to(&object_signer, ObjectController { extend_ref });
    // ...
  }

  entry fun add_message(
    caller: &signer,
    object: Object<MyStruct>,
    message: String
  ) acquires ObjectController {
    let caller_address = signer::address_of(caller);
    // 権限を処理するいくつかの方法があります

    // オブジェクトの所有者のみを許可
    assert!(object::is_owner(object, caller_address), E_NOT_OWNER);
    // コントラクトの公開者のみを許可
    assert!(caller_address == @my_addr, E_NOT_PUBLISHER);
    // または他の権限スキームを考えることもできます。可能性は無限大です！

    // extend_refを使用してsignerを取得
    let object_address = object::object_address(&object);
    let extend_ref = &borrow_global<ObjectController>(object_address).extend_ref;
    let object_signer = object::generate_signer_for_extending(extend_ref);

    // オブジェクトにメッセージを追加
    move_to(&object_signer, Message { message });
  }
}
```

### 転送の無効化/切り替え（`TransferRef`）

デフォルトでは、すべてのオブジェクトは転送可能です。これは`object::generate_transfer_ref`を使用して生成できる`TransferRef`を通じて変更できます。

以下の例は、オブジェクトが転送可能かどうかを決定するための権限を生成および管理する方法を示しています。

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object::{Self, Object};

  /// 呼び出し元がコントラクトの公開者ではありません
  const E_NOT_PUBLISHER: u64 = 1;

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct ObjectController has key {
    transfer_ref: object::TransferRef,
  }

  entry fun create_my_object(
    caller: &signer,
    transferrable: bool,
    controllable: bool
  ) {
    let caller_address = signer::address_of(caller);

    // オブジェクトを作成
    let constructor_ref = object::create_object(caller_address);

    // オブジェクトのsignerを取得
    let object_signer = object::generate_signer(&constructor_ref);

    // 転送を制御するためのtransfer_refを作成
    let transfer_ref = object::generate_transfer_ref(&constructor_ref);

    // ここで選択肢があります。オブジェクトを転送可能にし、
    // 後で変更を許可するかどうかを決定できます。デフォルトでは
    // 転送可能です
    if (!transferrable) {
      object::disable_ungated_transfer(&transfer_ref);
    };

    // 制御可能にしたい場合は、transfer_refを後で使用するために保存する必要があります
    if (controllable) {
      move_to(&object_signer, ObjectController { transfer_ref });
    }
    // ...
  }

  /// この例では、コントラクトの公開者のみが転送の
  /// 権限を変更できるようにします
  entry fun toggle_transfer(
    caller: &signer,
    object: Object<ObjectController>
  ) acquires ObjectController {
    // 公開者のみが転送を切り替えられるようにします
    let caller_address = signer::address_of(caller);
    assert!(caller_address == @my_addr, E_NOT_PUBLISHER);

    // transfer_refを取得
    let object_address = object::object_address(&object);
    let transfer_ref = &borrow_global<ObjectController>(
      object_address
    ).transfer_ref;

    // 現在の状態に基づいて切り替え
    if (object::ungated_transfer_allowed(object)) {
      object::disable_ungated_transfer(transfer_ref);
    } else {
      object::enable_ungated_transfer(transfer_ref);
    }
  }
}
```

### 一回限りの転送（`LinearTransferRef`）

さらに、作成者がすべての転送を制御したい場合、`TransferRef`から`LinearTransferRef`を作成して、一回限りの転送機能を提供できます。これは、オブジェクト作成者から受信者への一回限りの転送によって「ソウルバウンド」オブジェクトを作成するために使用できます。`LinearTransferRef`はオブジェクトの所有者が使用する必要があります。

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object::{Self, Object};

  /// 呼び出し元がコントラクトの公開者ではありません
  const E_NOT_PUBLISHER: u64 = 1;

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct ObjectController has key {
    transfer_ref: object::TransferRef,
  }

  entry fun create_my_object(
    caller: &signer,
  ) {
    let caller_address = signer::address_of(caller);

    // オブジェクトを作成
    let constructor_ref = object::create_object(caller_address);

    // オブジェクトのsignerを取得
    let object_signer = object::generate_signer(&constructor_ref);

    // 転送を制御するためのtransfer_refを作成
    let transfer_ref = object::generate_transfer_ref(&constructor_ref);

    // 無制限の転送を無効化
    object::disable_ungated_transfer(&transfer_ref);
    move_to(&object_signer, ObjectController {
      transfer_ref,
    });
    // ...
  }

  /// この例では、コントラクトの公開者のみが転送の
  /// 権限を変更できるようにします
  /// これで所有者は一度だけ転送できます
  entry fun transfer(
    caller: &signer,
    object: Object<ObjectController>,
    new_owner: address
  ) acquires ObjectController {
    // 公開者のみが転送を切り替えられるようにします
    let caller_address = signer::address_of(caller);
    assert!(caller_address == @my_addr, E_NOT_PUBLISHER);

    let object_address = object::object_address(&object);

    // transfer_refを取得
    let transfer_ref = &borrow_global<ObjectController>(
      object_address
    ).transfer_ref;

    // 一回限りの`LinearTransferRef`を生成
    let linear_transfer_ref = object::generate_linear_transfer_ref(
      transfer_ref
    );

    object::transfer_with_ref(linear_transfer_ref, new_owner);
  }
}
```

## オブジェクトの削除の許可（`DeleteRef`）

デフォルトの方法（削除を許可）で作成されたオブジェクトの場合、後で使用できる`DeleteRef`を生成できます。これにより、不要なものを削除し、ストレージの払い戻しを受けることができます。

削除不可能なオブジェクトに対して`DeleteRef`を作成することはできません。

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object::{Self, Object};

  /// 呼び出し元がオブジェクトの所有者ではありません
  const E_NOT_OWNER: u64 = 1;

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
  struct ObjectController has key {
    delete_ref: object::DeleteRef,
  }

  entry fun create_my_object(
    caller: &signer,
    _transferrable: bool,
    _controllable: bool
  ) {
    let caller_address = signer::address_of(caller);

    // オブジェクトを作成
    let constructor_ref = object::create_object(caller_address);

    // オブジェクトのsignerを取得
    let object_signer = object::generate_signer(&constructor_ref);

    // delete_refを作成して保存
    let delete_ref = object::generate_delete_ref(&constructor_ref);
    move_to(&object_signer, ObjectController {
      delete_ref
    });
    // ...
  }

  /// 所有者のみがオブジェクトを削除できるようにします
  entry fun delete(
    caller: &signer,
    object: Object<ObjectController>,
  ) acquires ObjectController {
    // 呼び出し元のみが削除できるようにします
    let caller_address = signer::address_of(caller);
    assert!(object::is_owner(object, caller_address), E_NOT_OWNER);

    let object_address = object::object_address(&object);

    // delete_refを取得します。消費されるため、リソースから
    // 抽出する必要があります
    let ObjectController {
      delete_ref
    } = move_from<ObjectController>(
      object_address
    );

    // オブジェクトを永久に削除！
    object::delete(delete_ref);
  }
}
```

## オブジェクトを不変にする

オブジェクトは、関連するコントラクトを不変にし、オブジェクトを拡張または変更する機能を削除することで不変にできます。デフォルトでは、コントラクトは不変ではなく、オブジェクトは`ExtendRef`で拡張でき、コントラクトが許可する場合はリソースを変更できます。

## さらなる読み物

すべての可能な`Ref`のドキュメントは、[こちら](https://aptos.dev/reference/move?branch=mainnet&page=aptos-framework/doc/object.md)の`0x1::object`のMoveリファレンスドキュメントで見つけることができます。

また、オブジェクトが構築された後の使用方法については[こちら](using-objects.mdx)で確認できます。
