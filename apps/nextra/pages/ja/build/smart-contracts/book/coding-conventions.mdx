# Moveコーディング規約

このセクションでは、Moveチームが有用と考えるMoveの基本的なコーディング規約を説明します。これらは推奨事項であり、他のフォーマットガイドラインや規約を好む場合は、それらを使用しても構いません。

## 命名規則

- **モジュール名**: 小文字のスネークケースを使用します。例：`fixed_point32`、`vector`。
- **型名**: ネイティブ型でない場合はキャメルケースを使用します。例：`Coin`、`RoleId`。
- **関数名**: 小文字のスネークケースを使用します。例：`destroy_empty`。
- **定数名**: エラーコードを表す場合は`E`で始まるアッパーキャメルケース（例：`EIndexOutOfBounds`）を使用し、エラー以外の値を表す場合はアッパースネークケース（例：`MIN_STAKE`）を使用します。
- **ジェネリック型名**: 適切な場合は説明的または反説明的な名前を使用します。例：Vectorのジェネリック型パラメータには`T`や`Element`を使用します。多くの場合、モジュール内の「メイン」の型はモジュール名と同じにします。例：`option::Option`、`fixed_point32::FixedPoint32`。
- **モジュールファイル名**: モジュール名と同じにします。例：`option.move`。
- **スクリプトファイル名**: 小文字のスネークケースを使用し、スクリプト内の「main」関数の名前と一致させます。
- **混合ファイル名**: ファイルに複数のモジュールやスクリプトが含まれる場合、ファイル名は小文字のスネークケースを使用し、内部の特定のモジュール/スクリプトの名前とは一致させません。

## インポート

- すべてのモジュールの`use`文はモジュールの先頭に配置します。
- 関数は宣言されたモジュールから完全修飾名で使用し、トップレベルではインポートしません。
- 型はトップレベルでインポートします。名前の衝突がある場合は、`as`を使用して適切に型をローカルでリネームします。

例えば、以下のようなモジュールがある場合：

```move
module 0x1::foo {
  struct Foo { }
  const CONST_FOO: u64 = 0;
  public fun do_foo(): Foo { Foo{} }
  // ...
}
```

これは以下のようにインポートして使用します：

```move
module 0x1::bar {
  use 0x1::foo::{Self, Foo};

  public fun do_bar(x: u64): Foo {
    if (x == 10) {
      foo::do_foo()
    } else {
      abort 0
    }
  }
  // ...
}
```

また、2つのモジュールをインポートする際に名前の衝突がある場合：

```move
module 0x1::other_foo {
  struct Foo {}
  // ...
}

module 0x1::importer {
  use 0x1::other_foo::Foo as OtherFoo;
  use 0x1::foo::Foo;
  // ...
}
```

## コメント

- 各モジュール、構造体、公開関数の宣言にはコメントを付けます。
- Moveには、ドキュメントコメント`///`、通常の単行コメント`//`、ブロックコメント`/* */`、ブロックドキュメントコメント`/** */`があります。

### コメントの例

ドキュメントコメントは、コメントの対象となる項目の直上に配置する必要があります。例えば、以下は有効です：

```move
/// My awesome module, doc comment can be used here
module 0x42::example { // double slash can be anywhere

  // Double slash can be anywhere

  /// My awesome constant
  const MY_VALUE: u64 = 5;

  /// My awesome error message
  const E_MY_ERROR: u64 = 10;

  #[view]
  /// My awesome view function
  fun show_me_the_money() {
    // ...
  }

  /* Similarly block comments can be anywhere */
}
```

以下は、ドキュメントコメント`///`が失敗する例です：

```move
module 0x42::example {

  /// My awesome view function <- アノテーションの下、コメントされる対象の直上にある必要があります
  #[view]
  fun show_me_the_money() {
    // ...
    /// 関数内
  }

  /// 何にも紐付いていない
}
```

## フォーマット

Moveチームは、フォーマット規約を強制するオートフォーマッタを作成する予定です。それまでの間：

- `script`と`address`ブロックの内容はインデントしないことを除き、4スペースのインデントを使用します。
- 行が100文字を超える場合は改行します。
- 構造体と定数は、モジュール内のすべての関数の前に宣言します。
