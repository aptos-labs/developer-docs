# 条件分岐

`if`式は、特定の条件が真の場合にのみコードを評価するように指定します。例：

```move
script {
  fun example() {
    if (x > 5) x = x - 5
  }
}
```

条件は`bool`型の式でなければなりません。

`if`式はオプションで`else`節を含むことができ、条件が偽の場合に評価する別の式を指定できます。

```move
script {
  fun example() {
    if (y <= 10) y = y + 1 else y = 10
  }
}
```

「真」の分岐または「偽」の分岐のいずれかが評価されますが、両方が評価されることはありません。どちらの分岐も単一の式または式ブロックにすることができます。

条件式は値を生成することができ、`if`式が結果を持つことができます。

```move
script {
  fun example() {
    let z = if (x < 100) x else 100;
  }
}
```

真の分岐と偽の分岐の式は互換性のある型を持つ必要があります。例：

```move
script {
  fun example() {
    // xとyはu64整数でなければなりません
    let maximum: u64 = if (x > y) x else y;

    // エラー！分岐の型が異なります
    let z = if (maximum < 10) 10u8 else 100u64;

    // エラー！分岐の型が異なります。デフォルトの偽の分岐は()でu64ではありません
    if (maximum >= 10) maximum;
  }
}
```

`else`節が指定されていない場合、偽の分岐はデフォルトでユニット値になります。以下は同等です：

```move
script {
  fun example() {
    if (condition) true_branch // 暗黙のデフォルト: else ()
    if (condition) true_branch else ()
  }
}
```

一般的に、[`if`式](conditionals.mdx)は式ブロックと組み合わせて使用されます。

```move
script {
  fun example() {
    let maximum = if (x > y) x else y;
    if (maximum < 10) {
        x = x + 10;
        y = y + 10;
    } else if (x >= 10 && y >= 10) {
        x = x - 10;
        y = y - 10;
    }
  }
}

```

## 条件分岐の文法

> _if-expression_ → **if (** _expression_ **)** _expression_ _else-clause_<sub>_opt_</sub>

> _else-clause_ → **else** _expression_
